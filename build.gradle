plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id "maven-publish"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'at.ac.tuwien.ifs.sge'
version '1.0.2'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}


dependencies {

    implementation 'info.picocli:picocli:4.6.1'
    //   compile 'org.jgrapht:jgrapht-core:1.3.0'
    implementation 'org.fusesource.jansi:jansi:2.3.4'
    implementation 'com.google.guava:guava:30.1.1-jre'
    testImplementation 'junit:junit:4.13.2'
}

jar {
    manifest {
        attributes 'Main-Class': 'at.ac.tuwien.ifs.sge.engine.cli.SgeCommand'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    archiveClassifier.set('exe')
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Entze/Strategy-Game-Engine")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "sge"
                description = "The core engine library, needed to implement games and agents."
                licenses {
                    license {
                        name = "GNU Affero General Public License version 3"
                        url = "https://opensource.org/licenses/agpl-v3.html"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "entze"
                        name = "Lukas Grassauer"
                        email = "entze@grassauer.eu"
                    }
                }

                scm {
                    url = "https://github.com/Entze/strategy-game-engine"
                    connection = "https://github.com/Entze/strategy-game-engine.git"
                    developerConnection = "git@github.com:Entze/strategy-game-engine.git"
                }
            }
        }
    }
}

test {

     useJUnit()
     maxHeapSize = '1G'
}